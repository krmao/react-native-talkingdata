buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:${rootProject.ext.has("androidToolsBuildGradleVersion") ? rootProject.ext.get("androidToolsBuildGradleVersion") : "7.3.1"}")
    }
}

apply plugin: 'com.android.library'

def safeExtGet(String prop, def fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def enableTalkingDataGooglePlay = safeExtGet('enableTalkingDataGooglePlay', false)

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 33)
    buildToolsVersion safeExtGet("buildToolsVersion", '33.0.0')
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 33)
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "enableTalkingDataGooglePlay", enableTalkingDataGooglePlay.toString()
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            if (enableTalkingDataGooglePlay) {
                java.srcDirs = ['src/main/java-googleplay'];
            } else {
                java.srcDirs = ['src/main/java']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    lintOptions {
        disable 'GradleCompatible'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    api 'com.facebook.react:react-native:+'
    api files(enableTalkingDataGooglePlay ? 'libs/GooglePlay_TalkingDataSDK_Android_V5.0.12.jar' : 'libs/SaaS_TalkingDataSDK_Android_V5.0.18.jar')
}
